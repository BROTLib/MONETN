<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.9">
  <POU Name="TelescopeControl" Id="{6a4f2e3a-c9ee-4352-b4f6-4f9d94b2c472}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM TelescopeControl
VAR_INPUT
(*
TCS interface section
see https://gitlab.aip.de/bmk10k/telescope_firmware/snippets/5
*)
	
	// boolean, true: Power on, false: power off
	power: 		BOOL;
	// boolean, true: go to home position, unset track/goto if active
	gohome: 	BOOL;
	// boolean, true: go into park position, ready to stow telescope, unset track/goto
	park: 		BOOL;
	// boolean, TRUE: MOVE telescope TO last ra/de AND start tracking. Set 'goto' TO FALSE, IF TRUE. FALSE: Stop tracking as soon as possible
	track: 		BOOL;
	// boolean, TRUE: MOVE telescope TO tau/de AND stop there. Set track TO FALSE, IF TRUE. FALSE: no effect.)
	goto:		BOOL;	
	// move the telescope to a given az-alt position
	slew:		BOOL;
	// boolean, TRUE: Stop motion OF telescope immediately, set goto AND track TO FALSE. FALSE: Allow motion OF telescope again via track/goto).
	stop:		BOOL;	
	// initiates hard-reset on all axis
	reset:		BOOL;
	// apparent right ascension, degrees, double
	rightascension:		LREAL := (4.0 + 35.0/60.0 + 54.09/3600.0) *15.0;	
	// apparent declination, degrees, double
	declination: 		LREAL := 16.0 + 30.0/60.0 + 25.4/3600.0;
	// desired elevation of the telescope pointing 
	elevation:	LREAL := 45.0;
	// desired azimut of the telescope pointing 
	azimuth:	LREAL := 320.0;
	// absolute offset for elevation
	elevation_offset:	LREAL := 0.0;
	// absolute offset for azimuth
	azimuth_offset : 	LREAL := 0.0;
	// absolute offset for time
	time_offset: 		LREAL := 0.0;
	// offset for the de-rotator
	derotator_offset:	LREAL := 180.0;
	// Nasmyth port 1 or 2
	Nasmyth_port: USINT;
	// Focus position in mm
	focus_position: LREAL := 77.000;
END_VAR
VAR
	// actual command from the TCS
	TCS_command: 	E_TCSCommand;
	// current Julian Date calculated from system time
	jd:				LREAL;
	// image derotation
	derotation_calc:	LREAL;
	// local siderial time calculated from Julian date
	lst:			LREAL;
	// elevation calulated when ra and dec are set by TCS
	elevation_calc:	LREAL; 
	// azimuth calulated when ra and dec are set by TCS
	azimuth_calc:	LREAL;
	// right ascension calulated when azimuth and elevation are set by TCS
	rightascension_calc:	LREAL;
	// declination calulated when azimuth and elevation are set by TCS
	declination_calc:		LREAL;
	// hour angle
	Hourangle_calc: LREAL;
	// precise astronomical	time
	fbTime: FB_AstroClock;
	
	eq2hor2:	FB_EQ2HOR2;
	hor2eq:		FB_HOR2EQ;
	
	// triggers power-failure event
	powerfailure: 	BOOL;
	// triggers power warning
	poweronwarning:	TP;
	// triggers a reset on power on
	poweron_reset:	TP;
	// power on timeout
	power_timeout:	TON;
	
	
	TCSpowerEvent,
	TCSgohomeEvent,
	TCSparkEvent,
	TCSgotoEvent,
	TCStrackEvent,
	TCSstopEvent,
	AltitudeEvent,
	TCSreadyEvent: FB_Eventlog;
	FocusDelay: TON := (PT:=T#5S);
	MQTTTimer : TON := (PT:=T#5S);
	// rotational velocity of the derotator
	DerotatorVelocity: LREAL;
	// rotational velocity of the elevation
	ElevationVelocity: LREAL;
	// rotational velocity of the elevation
	AzimuthVelocity: LREAL;
	// sign of derotation
	derotatation_sign: LREAL := 1.0;
	StatusWord, LastStatus:	UDINT;
END_VAR
VAR_OUTPUT
	//		TRUE, IF telescope can be operated
	ready:		BOOL; 
	// indicates an error on one of the components
	error:		BOOL;	
	//error id of axis
	errorid:	UDINT;	
	//TRUE, IF telescope is in fast-MOVE phase (includes pole-swap)
	sliding:	BOOL;	
	//TRUE, IF telescope is on tracking position AND currently tracking.
	tracking:	BOOL;	
	//TRUE, IF telescope is AT rest.
	stopped:	BOOL;	
	// (all degrees, azimuth 0=S, W>0)
	//racurrent: LREAL; 
	elevation_current:	LREAL;
	azimuth_current:	LREAL;
	//	(time in sec. to reach the goto/track position)
	slewtime:			LREAL; 
	// True: telescope is calibrated
	homed:				BOOL;
END_VAR
VAR CONSTANT
// STELLA1
	// telescope site longitude, source: Google 
	longitude: 		LREAL := -16.50926; //13.101867;
	// telescope site latitude, source: Google
	latitude: 		LREAL := 28.30120; //52.404940;
	// telescope site height above sea level: estimated
	altitude: 		LREAL := 2390.0;
	elevation_park: LREAL := 70.0;
	azimuth_park: 	LREAL := 359.999;
	derotator_park: LREAL := 229.85;

	elevation_home: LREAL := 45.0;
	azimuth_home: 	LREAL := 180.0;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[TelescopeAuxiliary();

// get the system time
fbTime();
jd := DateTime2JD(fbTime.time_RTCEX2);
lst := CT2LST(longitude, jd);
// set reset state either by hardware (key pressed) or by software (set by e.g. tcs)
IF GVL_Main.reset_switch THEN
	reset := TRUE;
END_IF


IF ElevationControl.error THEN
	errorid := ElevationControl.ErrorID;
	ElevationControl.reset := reset;
ELSIF AzimuthElevationControl.error THEN
	errorid := AzimuthElevationControl.ErrorID;
	AzimuthElevationControl.Reset := reset;
ELSIF FilterWheelControl.Error THEN
	errorid := FilterWheelControl.ErrorID;
	FilterWheelControl.Reset := reset;
ELSIF Focuscontrol.Error THEN
	errorid := Focuscontrol.ErrorID;
	FocusControl.reset := reset;
ELSIF DerotatorControl.Error THEN
	errorid := DerotatorControl.ErrorID;
	DerotatorControl.reset := reset;
END_IF

error := ElevationControl.error OR 
		 AzimuthElevationControl.error OR
		 FilterWheelControl.Error OR
		 Focuscontrol.Error OR
		 DerotatorControl.Error;

slewtime := MAX(MAX(ElevationControl.slewtime, AzimuthElevationControl.slewtime), DerotatorControl.slewtime);
			
IF error THEN
	gohome := 	FALSE;
	park  := 	FALSE;
	track := 	FALSE;
	goto := 	FALSE;
	slew := 	FALSE;
END_IF
		 
IF ElevationControl.error THEN
	ElevationControl.enable := FALSE;
	AzimuthElevationControl.enable := FALSE;
	HydraulicsControl.close_brake();
	power := FALSE;
END_IF

eq2hor2(alpha := rightascension,
	delta := declination,
	lon := longitude,
	lat := latitude,
	JD := jd + time_offset,
	alt => elevation_calc,
	az => azimuth_calc,
	ha => hourangle_calc);

// calculate derotator position 
derotation_calc := F_DerotatorPosition2(azimuth_calc, elevation_calc, declination_calc, latitude);
// apply offsets
elevation_calc := elevation_calc + elevation_offset;
azimuth_calc := azimuth_calc + azimuth_offset;
derotation_calc := derotatation_sign*(derotation_calc + derotator_offset);

// get current coordinates	
azimuth_current := AzimuthElevationControl.act_position;
elevation_current := ElevationControl.actual_position;

// calculate equatorial coordinates
hor2eq(alt := elevation_current, 
		az := azimuth_current,
		jd := jd,
		lat := latitude,
		lon := longitude,
		altitude := altitude,
		ra => rightascension_calc,
		dec => declination_calc);		

IF track THEN
	DerotatorVelocity := F_Derotatorvelocity(elevation_calc, azimuth_calc, latitude);
	ElevationVelocity := F_Elevationvelocity(elevation_calc, azimuth_calc, latitude);
	AzimuthVelocity := F_Azimuthvelocity(elevation_calc, azimuth_calc, latitude);
ELSE
	ElevationControl.Tracking := FALSE;
	ElevationControl.Velocity := 10.0;
	AzimuthElevationControl.Tracking := FALSE;
	AzimuthElevationControl.Velocity := 10.0;
	DerotatorControl.Tracking := FALSE;
	DerotatorControl.Velocity := 10.0;
END_IF
		
poweronwarning(IN := power OR (NOT ElevationControl.enable AND HydraulicsControl.brake_open), PT := T#250MS);
poweron_reset(IN := power AND NOT PendantControl.manual, PT := T#50MS);
// park the telescope after 12 hours
power_timeout(IN := power, PT := T#12H);
IF power_timeout.Q THEN
	power := FALSE;
	park := TRUE;
END_IF
// reset fan
CoverControl.reset := poweron_reset.Q OR reset;

IF Nasmyth_port = 1 THEN
	NasmythControl.right := FALSE;
	IF NasmythControl.Nasmyth_left THEN
		NasmythControl.left := FALSE;
		NasmythControl.enable := FALSE;
	ELSE
		NasmythControl.left := TRUE;
		NasmythControl.enable := TRUE;
	END_IF
ELSIF Nasmyth_port = 2 THEN
	NasmythControl.left := FALSE;
    IF NasMythControl.Nasmyth_right THEN
		NasmythControl.right := FALSE;
		NasmythControl.enable := FALSE;
	ELSE
		NasmythControl.right := TRUE;
		NasmythControl.enable := TRUE;
	END_IF
ELSE
	NasmythControl.right := FALSE;
	NasmythControl.left := FALSE;
	NasmythControl.enable := FALSE;	
END_IF
	
		
// set TCS_command according to state variables ordered by precedence
IF power THEN
	TCS_command := E_TCSCommand.poweron;
ELSIF track THEN
	TCS_command := E_TCSCommand.track;
	goto := FALSE;
	slew := FALSE;
ELSIF goto THEN
	TCS_command := E_TCSCommand.goto;
	track := FALSE;
	slew := FALSE;
ELSIF gohome THEN
	TCS_command := E_TCSCommand.gohome;
	goto := FALSE;
	track := FALSE;
	slew := FALSE;
ELSIF slew THEN
	TCS_command := E_TCSCommand.slew;
	goto := FALSE;
	track := FALSE;
ELSIF park THEN
	TCS_command := E_TCSCommand.park;
	goto := FALSE;
	track := FALSE;
	slew := FALSE;
ELSIF stop THEN
	TCS_command := E_TCSCommand.stop;
	goto := FALSE;
	track := FALSE;
	slew := FALSE;
	gohome := FALSE;
	park := FALSE;
ELSE
	TCS_command := E_TCSCommand.no_command;
	ElevationControl.MoveAxis := FALSE;
	AzimuthElevationControl.MoveAxis := FALSE;
END_IF


IF TCS_command = E_TCSCommand.poweron THEN
	poweron();
ELSIF TCS_command = E_TCSCommand.park THEN
	parktelescope();
ELSIF TCS_command = E_TCSCommand.gohome THEN
	 hometelescope();
ELSIF TCS_command = E_TCSCommand.track THEN
	tracktelescope();
ELSIF TCS_command = E_TCSCommand.goto THEN
	gototelescope();
ELSIF TCS_command = E_TCSCommand.slew THEN
	slewtelescope();
END_IF
 
sliding := ElevationControl.MoveAxis OR 
			AzimuthElevationControl.MoveAxis OR
			DerotatorControl.MoveAxis;
			
tracking := ElevationControl.Tracking AND 
			AzimuthElevationControl.Tracking AND
			DerotatorControl.Tracking;
			
stopped :=  ElevationControl.ElevationAxis.StandStill AND 
			AzimuthElevationControl.AzimuthAxis.StandStill AND
			DerotatorControl.DerotatorAxis.StandStill;
			
homed := ElevationControl.Calibrated AND
			AzimuthElevationControl.calibrated AND
			FilterWheelControl.Calibrated AND
			FocusControl.Calibrated AND
			DerotatorControl.Calibrated;
			
ready := homed AND 
	CoverControl.isopen AND 
	ElevationControl.enable AND
	AzimuthElevationControl.enable AND
	DerotatorControl.enable AND NOT error;

IF NOT error THEN
	reset := FALSE;
END_IF

IF SafetyHandling.Emergency_stop THEN
	ElevationControl.enable := FALSE;
	AzimuthElevationControl.enable := FALSE;
	DerotatorControl.enable := FALSE;
END_IF

MQTTTimer(IN:=TRUE);
IF MQTTTimer.Q THEN // publish new payload every second
	MQTTTimer(IN:=FALSE);

	MAIN.MQTTClient.Publish('telescope', 'dome', 'Azimuth', LREAL_TO_FMTSTR(azimuth_current, 5, TRUE));
	MAIN.MQTTClient.Publish('telescope', 'dome', 'Elevation', LREAL_TO_FMTSTR(elevation_current, 5, TRUE));
	MAIN.MQTTClient.Publish('telescope', 'dome', 'HourAngle', LREAL_TO_FMTSTR(hourangle_calc, 5, TRUE));
	MAIN.MQTTClient.Publish('telescope', 'dome', 'RightAscension', LREAL_TO_FMTSTR(rightascension_calc, 5, TRUE));
	MAIN.MQTTClient.Publish('telescope', 'dome', 'Declination', LREAL_TO_FMTSTR(declination_calc, 5, TRUE));
	MAIN.MQTTClient.Publish('telescope', 'dome', 'Derotator', LREAL_TO_FMTSTR(derotation_calc, 5, TRUE));
	MAIN.MQTTClient.Publish('telescope', 'dome', 'errorid',		UDINT_TO_STRING(errorid));
	MAIN.MQTTClient.Publish('telescope', 'dome', 'error',		BOOL_TO_STRING(error));
	MAIN.MQTTClient.Publish('telescope', 'dome', 'ready',		BOOL_TO_STRING(ready));
	MAIN.MQTTClient.Publish('telescope', 'dome', 'sliding',		BOOL_TO_STRING(sliding));
	MAIN.MQTTClient.Publish('telescope', 'dome', 'tracking', 	BOOL_TO_STRING(tracking));
	MAIN.MQTTClient.Publish('telescope', 'dome', 'stopped',		BOOL_TO_STRING(stopped));
	MAIN.MQTTClient.Publish('telescope', 'dome', 'homed',		BOOL_TO_STRING(homed));
END_IF

StatusWord.0 := error;
StatusWord.1 := ready;
StatusWord.2 := sliding;
StatusWord.3 := tracking;
StatusWord.4 := stopped;
StatusWord.5 := homed;
StatusWord.6 := power;
StatusWord.7 := park;
StatusWord.8 := gohome;
StatusWord.9 := track;
StatusWord.10 := goto;
StatusWord.11 := slew;
StatusWord.12 := stop;

IF StatusWord<>LastStatus THEN // publish new payload every second
	LastStatus := StatusWord;
	MAIN.MQTTClient.Publish('telescope', 'dome', 'error',		BOOL_TO_STRING(error));
	MAIN.MQTTClient.Publish('telescope', 'dome', 'ready',		BOOL_TO_STRING(ready));
	MAIN.MQTTClient.Publish('telescope', 'dome', 'sliding',		BOOL_TO_STRING(sliding));
	MAIN.MQTTClient.Publish('telescope', 'dome', 'tracking', 	BOOL_TO_STRING(tracking));
	MAIN.MQTTClient.Publish('telescope', 'dome', 'stopped',		BOOL_TO_STRING(stopped));
	MAIN.MQTTClient.Publish('telescope', 'dome', 'homed',		BOOL_TO_STRING(homed));
	MAIN.MQTTClient.Publish('telescope', 'dome', 'power',		BOOL_TO_STRING(power));
	MAIN.MQTTClient.Publish('telescope', 'dome', 'park',		BOOL_TO_STRING(park));
	MAIN.MQTTClient.Publish('telescope', 'dome', 'gohome',		BOOL_TO_STRING(gohome));
	MAIN.MQTTClient.Publish('telescope', 'dome', 'track',		BOOL_TO_STRING(track));
	MAIN.MQTTClient.Publish('telescope', 'dome', 'goto',		BOOL_TO_STRING(goto));
	MAIN.MQTTClient.Publish('telescope', 'dome', 'slew',		BOOL_TO_STRING(slew));
	MAIN.MQTTClient.Publish('telescope', 'dome', 'stop',		BOOL_TO_STRING(stop));
END_IF


(* Event function blocks start here. *)
TCSpowerEvent(	Trigger := powerfailure, 
				Level := ADSLOG_MSGTYPE_ERROR,
				FormatString := 'power %s',
				OnMessage := 'Failure',
				OffMessage := 'OK');

TCSgohomeEvent(	Trigger := gohome, 
				Level := ADSLOG_MSGTYPE_HINT,
				FormatString := 'GOHOME %s',
				OnMessage := 'commencing',
				OffMEssage := 'completed');
TCSparkEvent(	Trigger := park, 
				Level := ADSLOG_MSGTYPE_HINT,
				FormatString := 'PARK %s',
				OnMessage := 'commencing',
				OffMEssage := 'completed');
TCSgotoEvent(	Trigger := goto, 
				Level := ADSLOG_MSGTYPE_HINT,
				FormatString := 'GOTO %s',
				OnMessage := 'commencing',
				OffMEssage := 'completed');
TCStrackEvent(	Trigger := track, 
				Level := ADSLOG_MSGTYPE_HINT,
				FormatString := 'TRACK %s',
				OnMessage := 'commencing',
				OffMEssage := 'completed');
TCSstopEvent(	Trigger := stop, 
				Level := ADSLOG_MSGTYPE_WARN,
				FormatString := 'STOP %s',
				OnMessage := 'commencing',
				OffMEssage := 'completed',
				OffLevel := ADSLOG_MSGTYPE_HINT);
AltitudeEvent(	Trigger := power AND elevation_calc<0.0, 
				Level := ADSLOG_MSGTYPE_WARN,
				FormatString := '%s',
				OnMessage := 'Target below horizon',
				OffMEssage := '');
TCSreadyEvent(	Trigger := power AND ready, 
				Level := ADSLOG_MSGTYPE_HINT,
				FormatString := '%s',
				OnMessage := 'STELLA1 startup finished');]]></ST>
    </Implementation>
    <Method Name="gototelescope" Id="{c7053474-39ba-4cc3-ab24-2521b02cf8f4}">
      <Declaration><![CDATA[METHOD gototelescope: USINT
VAR_INPUT
END_VAR
VAR
	GoToEvent:	FB_Eventlog;
END_VAR
VAR_STAT
	Stage:		USINT := 0;
	LastStage:	USINT := 101;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[gototelescope := Stage;
CASE Stage OF
	0:
		ElevationControl.position := elevation_calc;
		AzimuthElevationControl.position := azimuth_calc;
		DerotatorControl.position := derotation_calc;
		DerotatorControl.Velocity := 10.0;
		ElevationControl.velocity := 10.0;
		AzimuthElevationControl.velocity := 10.0;
		
		ElevationControl.MoveAxis := TRUE;
		AzimuthElevationControl.MoveAxis := TRUE;
		DerotatorControl.MoveAxis := TRUE;
		IF ElevationControl.ElevationAxis.Busy AND
			AzimuthElevationControl.AzimuthAxis.Busy AND
			DerotatorControl.DerotatorAxis.Busy THEN
				Stage := 50;
		END_IF
	50:
		IF NOT ElevationControl.ElevationAxis.Busy OR
		   ElevationControl.ElevationAxis.MoveDone THEN
			ElevationControl.MoveAxis := FALSE;
		END_IF
		IF NOT AzimuthElevationControl.AzimuthAxis.Busy OR
		   AzimuthElevationControl.AzimuthAxis.MoveDone THEN
			AzimuthElevationControl.MoveAxis := FALSE;
		END_IF
		IF NOT DerotatorControl.DerotatorAxis.Busy OR 
		   DerotatorControl.DerotatorAxis.MoveDone THEN
			DerotatorControl.MoveAxis := FALSE;
		END_IF
		IF NOT sliding OR stopped THEN // all axis standstill
			Stage := 100;
		END_IF
	100:
		goto := FALSE;
		gototelescope := 100;
		Stage := 0;
END_CASE
GoToEvent(Trigger := Stage <> LastStage, FormatString := 'GoTo progress: %s', OnMessage := USINT_TO_STRING(Stage));
LastStage := Stage;]]></ST>
      </Implementation>
    </Method>
    <Method Name="hometelescope" Id="{2c96c3e4-ffc4-4f92-b3af-46d302446b40}">
      <Declaration><![CDATA[METHOD hometelescope : BOOL
VAR_INPUT
END_VAR

VAR
	HomeEvent: FB_Eventlog;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[ElevationControl.position := elevation_home;
ElevationControl.MoveAxis := TRUE;
AzimuthElevationControl.position := azimuth_home;
AzimuthElevationControl.MoveAxis := TRUE;
IF ElevationControl.ElevationAxis.MoveDone AND 
	AzimuthElevationControl.AzimuthAxis.MoveDone THEN
	gohome := FALSE;
	hometelescope := TRUE;
ELSE
	hometelescope := FALSE;
END_IF

HomeEvent(Trigger := hometelescope, 
			Level := ADSLOG_MSGTYPE_HINT,
			FormatString := '%s',
			OnMessage := 'Telescope is homed.');]]></ST>
      </Implementation>
    </Method>
    <Method Name="parktelescope" Id="{99ae1f40-9696-469a-b290-774914abbb25}">
      <Declaration><![CDATA[METHOD parktelescope: USINT
VAR_INPUT
END_VAR
VAR
	ParkEvent:	FB_Eventlog;
END_VAR
VAR_STAT
	Stage:		USINT := 0;
	LastStage:	USINT := 101;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[parktelescope := Stage;
CASE Stage OF
	0: 
		gohome := 	FALSE;
		track := 	FALSE;
		goto := 	FALSE;
		slew := 	FALSE;
		IF FocusControl.enable THEN
			Focuscontrol.enable := FALSE;
		END_IF
		// disable filterwheel
		FilterWheelControl.Enable := FALSE;
		Stage := 25;
	25:
		ElevationControl.position := elevation_park;
		ElevationControl.velocity := 10.0;
		ElevationControl.MoveAxis := TRUE;
		AzimuthElevationControl.position := azimuth_park;
		AzimuthElevationControl.velocity := 10;
		AzimuthElevationControl.MoveAxis := TRUE;
		DerotatorControl.position := derotator_park;
		DerotatorControl.Velocity := 10.0;
		DerotatorControl.MoveAxis := TRUE;
		Stage := 50;
	50:
		IF NOT ElevationControl.ElevationAxis.Busy AND 
			ElevationControl.ElevationAxis.MoveDone AND 
			NOT AzimuthElevationControl.AzimuthAxis.Busy AND
			AzimuthElevationControl.AzimuthAxis.MoveDone THEN 
				ElevationControl.enable := FALSE;
				AzimuthElevationControl.Enable := FALSE;
		END_IF
		IF NOT DerotatorControl.DerotatorAxis.Busy AND 
			DerotatorControl.DerotatorAxis.MoveDone THEN
				DerotatorControl.enable := FALSE;
		END_IF
		IF NOT ElevationControl.enable AND 
		   NOT AzimuthElevationControl.Enable AND 
		   NOT DerotatorControl.enable THEN
			Stage := 75;
		END_IF
	75:
		IF HydraulicsControl.brake_closed THEN
			CoverControl.close := TRUE;			
		END_IF
		IF CoverControl.isclosed THEN
			Stage := 100;
		END_IF
	100:
			park := FALSE;
			parktelescope := 100;
			Stage := 0;	
END_CASE
ParkEvent(Trigger := Stage <> LastStage, FormatString := 'Parking progress: %s', OnMessage := USINT_TO_STRING(Stage));
LastStage := Stage;]]></ST>
      </Implementation>
    </Method>
    <Method Name="poweron" Id="{34114692-caf6-4cf5-8270-8b1671f5dd0a}">
      <Declaration><![CDATA[METHOD poweron : BOOL
VAR_INPUT
END_VAR
VAR
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF NOT Main.brake_clearing THEN
	powerfailure := TRUE;
	TCSpowerEvent.OnMessage := 'failure: brake clearing not set';
	power := FALSE;
ELSIF NOT Transformer.power_OK THEN
	powerfailure := TRUE;
	TCSpowerEvent.OnMessage := 'failure: main power failure';
	power := FALSE;
ELSIF NOT Transformer.phase_available THEN
	powerfailure := TRUE;
	TCSpowerEvent.OnMessage := 'failure: hydraulics power failure';
	power := FALSE;
ELSE
	powerfailure := FALSE;		
END_IF
// COVER MUST BE OPENED FOR A SUCCESSFUL WAKE & SHAKE COMMAND!
Covercontrol.open := TRUE;

// enable elevation, enable azimuth
IF MAIN.ready AND CoverControl.isopen THEN
	ElevationControl.enable := TRUE;
	AzimuthElevationControl.enable := TRUE;
END_IF

IF ElevationControl.Ready AND NOT ElevationControl.Calibrated THEN
	ElevationControl.HomeAxis := TRUE;
END_IF

IF AzimuthElevationControl.ready AND NOT AzimuthElevationControl.calibrated THEN
	AzimuthElevationControl.HomeAxis := TRUE;
END_IF


// enable derotator
IF ElevationControl.Ready THEN
	DerotatorControl.enable := TRUE;
	DerotatorControl.HomeAxis := DerotatorControl.Ready AND NOT DerotatorControl.Calibrated;
END_IF

// enable filterwheel after Derotator Axis to avoid additional torque
IF DerotatorControl.Calibrated THEN
	FilterWheelControl.Enable := TRUE;
	FilterWheelControl.HomeAxis := NOT FilterWheelControl.Calibrated;
END_IF

// enable focus
FocusControl.enable := TRUE;
IF FocusControl.ready AND NOT FocusControl.Calibrated THEN
	FocusControl.HomeAxis := TRUE;
END_IF

IF CoverControl.isopen AND 
	ElevationControl.Calibrated AND
	AzimuthElevationControl.calibrated AND
	DerotatorControl.Calibrated AND
	FilterWheelControl.Calibrated THEN
	power := FALSE;
	poweron := TRUE;
ELSE
	poweron := FALSE;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="slewtelescope" Id="{e66a400f-15f7-407b-9ae1-f4d90da8099d}">
      <Declaration><![CDATA[METHOD slewtelescope : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[ElevationControl.position := elevation;
ElevationControl.velocity := 10.0;
ElevationControl.MoveAxis := TRUE;
AzimuthElevationControl.position := azimuth;
AzimuthElevationControl.velocity := 10.0;
AzimuthElevationControl.MoveAxis := TRUE;
DerotatorControl.position := elevation + derotator_offset;
DerotatorControl.Velocity := 10.0;
DerotatorControl.MoveAxis := TRUE;
IF ElevationControl.ElevationAxis.MoveDone 
	AND AzimuthElevationControl.AzimuthAxis.MoveDone 
	AND DerotatorControl.DerotatorAxis.MoveDone THEN
	ElevationControl.MoveAxis := FALSE;
	AzimuthElevationControl.MoveAxis := FALSE;
	DerotatorControl.MoveAxis := FALSE;
	slew := FALSE;
	slewtelescope := TRUE;
ELSE
	slewtelescope := FALSE;
END_IF

]]></ST>
      </Implementation>
    </Method>
    <Method Name="tracktelescope" Id="{8d4fcfbf-479d-4826-a883-1021b018a460}">
      <Declaration><![CDATA[METHOD tracktelescope : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[ElevationControl.position := 		elevation_calc;
AzimuthElevationControl.position := azimuth_calc;
DerotatorControl.position := 		derotation_calc;

DerotatorControl.Velocity := 		DerotatorVelocity;
ElevationControl.velocity := 		ElevationVelocity;
AzimuthElevationControl.velocity := AzimuthVelocity;

ElevationControl.Tracking := 		TRUE;
AzimuthElevationControl.Tracking := TRUE;
DerotatorControl.Tracking := 		TRUE;
	
]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="TelescopeControl">
      <LineId Id="5012" Count="1" />
      <LineId Id="58" Count="0" />
      <LineId Id="3290" Count="1" />
      <LineId Id="65" Count="0" />
      <LineId Id="628" Count="1" />
      <LineId Id="7470" Count="1" />
      <LineId Id="645" Count="0" />
      <LineId Id="4249" Count="1" />
      <LineId Id="7318" Count="0" />
      <LineId Id="4251" Count="0" />
      <LineId Id="4301" Count="0" />
      <LineId Id="7319" Count="0" />
      <LineId Id="4253" Count="0" />
      <LineId Id="4302" Count="0" />
      <LineId Id="7320" Count="0" />
      <LineId Id="4312" Count="0" />
      <LineId Id="4315" Count="0" />
      <LineId Id="7322" Count="0" />
      <LineId Id="4316" Count="1" />
      <LineId Id="7323" Count="0" />
      <LineId Id="4318" Count="0" />
      <LineId Id="4252" Count="0" />
      <LineId Id="6575" Count="4" />
      <LineId Id="6581" Count="0" />
      <LineId Id="6601" Count="0" />
      <LineId Id="6600" Count="0" />
      <LineId Id="6598" Count="0" />
      <LineId Id="4330" Count="0" />
      <LineId Id="6587" Count="4" />
      <LineId Id="6586" Count="0" />
      <LineId Id="6584" Count="0" />
      <LineId Id="4246" Count="1" />
      <LineId Id="5528" Count="1" />
      <LineId Id="6602" Count="0" />
      <LineId Id="4248" Count="0" />
      <LineId Id="1794" Count="0" />
      <LineId Id="1282" Count="0" />
      <LineId Id="1795" Count="3" />
      <LineId Id="2008" Count="2" />
      <LineId Id="7963" Count="0" />
      <LineId Id="7968" Count="0" />
      <LineId Id="7967" Count="0" />
      <LineId Id="7969" Count="0" />
      <LineId Id="3301" Count="0" />
      <LineId Id="7962" Count="0" />
      <LineId Id="7965" Count="0" />
      <LineId Id="7979" Count="0" />
      <LineId Id="7964" Count="0" />
      <LineId Id="3302" Count="1" />
      <LineId Id="7978" Count="0" />
      <LineId Id="3520" Count="0" />
      <LineId Id="2011" Count="0" />
      <LineId Id="3521" Count="4" />
      <LineId Id="3528" Count="1" />
      <LineId Id="6202" Count="0" />
      <LineId Id="6570" Count="1" />
      <LineId Id="7941" Count="0" />
      <LineId Id="7952" Count="0" />
      <LineId Id="6572" Count="0" />
      <LineId Id="7954" Count="6" />
      <LineId Id="6201" Count="0" />
      <LineId Id="4075" Count="0" />
      <LineId Id="4242" Count="0" />
      <LineId Id="6610" Count="0" />
      <LineId Id="6608" Count="1" />
      <LineId Id="6611" Count="0" />
      <LineId Id="7931" Count="0" />
      <LineId Id="6612" Count="0" />
      <LineId Id="4347" Count="0" />
      <LineId Id="4835" Count="0" />
      <LineId Id="6606" Count="1" />
      <LineId Id="7305" Count="0" />
      <LineId Id="7291" Count="1" />
      <LineId Id="7296" Count="0" />
      <LineId Id="7294" Count="0" />
      <LineId Id="7304" Count="0" />
      <LineId Id="7298" Count="0" />
      <LineId Id="7293" Count="0" />
      <LineId Id="7285" Count="0" />
      <LineId Id="7306" Count="0" />
      <LineId Id="7299" Count="0" />
      <LineId Id="7307" Count="1" />
      <LineId Id="7302" Count="0" />
      <LineId Id="7287" Count="0" />
      <LineId Id="7286" Count="0" />
      <LineId Id="7289" Count="0" />
      <LineId Id="7309" Count="0" />
      <LineId Id="7311" Count="2" />
      <LineId Id="7284" Count="0" />
      <LineId Id="3591" Count="0" />
      <LineId Id="3784" Count="0" />
      <LineId Id="1499" Count="0" />
      <LineId Id="8133" Count="1" />
      <LineId Id="1501" Count="0" />
      <LineId Id="1527" Count="0" />
      <LineId Id="4283" Count="0" />
      <LineId Id="6377" Count="0" />
      <LineId Id="1528" Count="1" />
      <LineId Id="4282" Count="0" />
      <LineId Id="6378" Count="0" />
      <LineId Id="1513" Count="0" />
      <LineId Id="1526" Count="0" />
      <LineId Id="4275" Count="1" />
      <LineId Id="6379" Count="0" />
      <LineId Id="6374" Count="2" />
      <LineId Id="6380" Count="0" />
      <LineId Id="1547" Count="1" />
      <LineId Id="4272" Count="1" />
      <LineId Id="6381" Count="0" />
      <LineId Id="1524" Count="1" />
      <LineId Id="4284" Count="1" />
      <LineId Id="6382" Count="0" />
      <LineId Id="4286" Count="1" />
      <LineId Id="1511" Count="0" />
      <LineId Id="1519" Count="0" />
      <LineId Id="4290" Count="1" />
      <LineId Id="1509" Count="0" />
      <LineId Id="4294" Count="0" />
      <LineId Id="4299" Count="0" />
      <LineId Id="8136" Count="1" />
      <LineId Id="3533" Count="0" />
      <LineId Id="7783" Count="0" />
      <LineId Id="3541" Count="0" />
      <LineId Id="4239" Count="0" />
      <LineId Id="3920" Count="0" />
      <LineId Id="7980" Count="0" />
      <LineId Id="3928" Count="0" />
      <LineId Id="6383" Count="1" />
      <LineId Id="7476" Count="0" />
      <LineId Id="3535" Count="0" />
      <LineId Id="3540" Count="0" />
      <LineId Id="3539" Count="0" />
      <LineId Id="7626" Count="0" />
      <LineId Id="7628" Count="0" />
      <LineId Id="7632" Count="0" />
      <LineId Id="4256" Count="0" />
      <LineId Id="7624" Count="1" />
      <LineId Id="7631" Count="0" />
      <LineId Id="4255" Count="0" />
      <LineId Id="7621" Count="1" />
      <LineId Id="7630" Count="0" />
      <LineId Id="7279" Count="3" />
      <LineId Id="4257" Count="0" />
      <LineId Id="7629" Count="0" />
      <LineId Id="7948" Count="0" />
      <LineId Id="7950" Count="1" />
      <LineId Id="7949" Count="0" />
      <LineId Id="6942" Count="0" />
      <LineId Id="7472" Count="3" />
      <LineId Id="7933" Count="0" />
      <LineId Id="7133" Count="0" />
      <LineId Id="7934" Count="0" />
      <LineId Id="7936" Count="1" />
      <LineId Id="7935" Count="0" />
      <LineId Id="4662" Count="3" />
      <LineId Id="4837" Count="1" />
      <LineId Id="4841" Count="0" />
      <LineId Id="4853" Count="0" />
      <LineId Id="4855" Count="0" />
      <LineId Id="4857" Count="0" />
      <LineId Id="6780" Count="0" />
      <LineId Id="7316" Count="0" />
      <LineId Id="7776" Count="5" />
      <LineId Id="4656" Count="0" />
      <LineId Id="7090" Count="0" />
      <LineId Id="7093" Count="4" />
      <LineId Id="7091" Count="0" />
      <LineId Id="8119" Count="6" />
      <LineId Id="7098" Count="3" />
      <LineId Id="7103" Count="4" />
      <LineId Id="8126" Count="6" />
      <LineId Id="7132" Count="0" />
      <LineId Id="7092" Count="0" />
      <LineId Id="3530" Count="1" />
      <LineId Id="3583" Count="2" />
      <LineId Id="6778" Count="0" />
      <LineId Id="5530" Count="0" />
      <LineId Id="3085" Count="0" />
      <LineId Id="931" Count="1" />
      <LineId Id="1179" Count="0" />
      <LineId Id="933" Count="3" />
      <LineId Id="1180" Count="0" />
      <LineId Id="937" Count="0" />
      <LineId Id="1183" Count="0" />
      <LineId Id="939" Count="1" />
      <LineId Id="1181" Count="0" />
      <LineId Id="941" Count="0" />
      <LineId Id="1184" Count="0" />
      <LineId Id="943" Count="1" />
      <LineId Id="1182" Count="0" />
      <LineId Id="945" Count="0" />
      <LineId Id="1185" Count="0" />
      <LineId Id="947" Count="2" />
      <LineId Id="1187" Count="0" />
      <LineId Id="1186" Count="0" />
      <LineId Id="1209" Count="0" />
      <LineId Id="1491" Count="4" />
      <LineId Id="6773" Count="2" />
      <LineId Id="3293" Count="0" />
    </LineIds>
    <LineIds Name="TelescopeControl.gototelescope">
      <LineId Id="96" Count="0" />
      <LineId Id="41" Count="1" />
      <LineId Id="45" Count="9" />
      <LineId Id="107" Count="1" />
      <LineId Id="110" Count="1" />
      <LineId Id="109" Count="0" />
      <LineId Id="55" Count="0" />
      <LineId Id="70" Count="0" />
      <LineId Id="104" Count="0" />
      <LineId Id="71" Count="0" />
      <LineId Id="74" Count="1" />
      <LineId Id="106" Count="0" />
      <LineId Id="78" Count="0" />
      <LineId Id="81" Count="0" />
      <LineId Id="84" Count="0" />
      <LineId Id="105" Count="0" />
      <LineId Id="87" Count="4" />
      <LineId Id="56" Count="0" />
      <LineId Id="93" Count="2" />
      <LineId Id="43" Count="0" />
      <LineId Id="97" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="TelescopeControl.hometelescope">
      <LineId Id="6" Count="4" />
      <LineId Id="17" Count="0" />
      <LineId Id="11" Count="0" />
      <LineId Id="20" Count="0" />
      <LineId Id="18" Count="1" />
      <LineId Id="12" Count="0" />
      <LineId Id="27" Count="3" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="TelescopeControl.parktelescope">
      <LineId Id="115" Count="0" />
      <LineId Id="166" Count="0" />
      <LineId Id="118" Count="0" />
      <LineId Id="122" Count="0" />
      <LineId Id="119" Count="2" />
      <LineId Id="123" Count="4" />
      <LineId Id="133" Count="0" />
      <LineId Id="132" Count="0" />
      <LineId Id="128" Count="0" />
      <LineId Id="143" Count="0" />
      <LineId Id="129" Count="1" />
      <LineId Id="144" Count="0" />
      <LineId Id="131" Count="0" />
      <LineId Id="140" Count="2" />
      <LineId Id="135" Count="0" />
      <LineId Id="134" Count="0" />
      <LineId Id="136" Count="0" />
      <LineId Id="177" Count="0" />
      <LineId Id="179" Count="0" />
      <LineId Id="178" Count="0" />
      <LineId Id="137" Count="0" />
      <LineId Id="149" Count="0" />
      <LineId Id="139" Count="0" />
      <LineId Id="150" Count="0" />
      <LineId Id="180" Count="0" />
      <LineId Id="151" Count="1" />
      <LineId Id="148" Count="0" />
      <LineId Id="181" Count="1" />
      <LineId Id="153" Count="1" />
      <LineId Id="116" Count="0" />
      <LineId Id="156" Count="2" />
      <LineId Id="184" Count="0" />
      <LineId Id="187" Count="0" />
      <LineId Id="183" Count="0" />
      <LineId Id="155" Count="0" />
      <LineId Id="188" Count="1" />
      <LineId Id="162" Count="0" />
      <LineId Id="117" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="169" Count="0" />
    </LineIds>
    <LineIds Name="TelescopeControl.poweron">
      <LineId Id="99" Count="14" />
      <LineId Id="117" Count="0" />
      <LineId Id="120" Count="0" />
      <LineId Id="166" Count="0" />
      <LineId Id="124" Count="0" />
      <LineId Id="126" Count="1" />
      <LineId Id="129" Count="28" />
      <LineId Id="167" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="168" Count="3" />
      <LineId Id="175" Count="1" />
      <LineId Id="173" Count="1" />
      <LineId Id="172" Count="0" />
    </LineIds>
    <LineIds Name="TelescopeControl.slewtelescope">
      <LineId Id="6" Count="0" />
      <LineId Id="31" Count="0" />
      <LineId Id="7" Count="1" />
      <LineId Id="34" Count="0" />
      <LineId Id="9" Count="1" />
      <LineId Id="33" Count="0" />
      <LineId Id="11" Count="7" />
      <LineId Id="21" Count="0" />
      <LineId Id="25" Count="1" />
      <LineId Id="19" Count="1" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="TelescopeControl.tracktelescope">
      <LineId Id="6" Count="2" />
      <LineId Id="16" Count="0" />
      <LineId Id="9" Count="2" />
      <LineId Id="17" Count="0" />
      <LineId Id="12" Count="3" />
      <LineId Id="5" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>