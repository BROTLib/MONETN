<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.9">
  <POU Name="MAIN" Id="{0fe844df-7078-44d3-bc48-268e6c994874}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM MAIN
VAR
	// indicates the power state for the AX5000
	power_OK:			BOOL;
	// indicates the phases for the hydraulics
	phase_available:	BOOL;
	// telescope drives are ready to enable
	ready:				BOOL;
	// trafo did not soft start
	power_failure:		BOOL;
	// error stat on drive, power or hydraulics
	error:				BOOL;
	// manual, panel or remote reset
	reset:				BOOL;
	// hydraulics warning 
	warning:			BOOL;
	// start hydraulics
	start:				BOOL;
	// stop hydraulics 
	stop:				BOOL;
	// soft start trafo
	key_switch:			BOOL;
	//	open brake
	brake_clearing:		BOOL := FALSE;
	// blink error lamp on warning
	warn_blink: 		FB_BLINK;
	// watchdog for trafo softstarter
	power_watchdog: 	TON;
	// main cabinet temperature in degrees
	cabinet_temperature:	REAL;
	// measures time between soft start command and power available
	ups_input_voltage:		REAL;
	ups_input_current:		REAL;
	ups_temperature:		REAL;
	ups_battery_temperature:		REAL;
	ups_output_voltage:		REAL;
	ups_output_current:		REAL;

	power_timer:		FB_Timer;
	PowerEvent,
	PhaseLog,
	PowerLog,
	TemperatureEvent:	FB_EventLog;
	//MQTTPublisher:		FB_MQTTPublisher;
	//MQTTTelegraf:		FB_MQTTTelegraf;
	MQTTTimer : 		TON := (PT:=T#5S);
	sPayloadPub: 		STRING(255);
	GetHostName:		FB_GetHostName;
	HostName: 			STRING(255);
	LightSwitch:		FB_ButtonEnable;
	Lightbutton: 		BOOL;
	bExecute:			BOOL := TRUE;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* 
Main control loop
* Execute sub modules
* power management
* control panel switches and status lamps

* start switch activates Hydraulics and Trafo
* 
*)
GetHostName(bExecute := bExecute, sHostName => HostName);
bExecute := FALSE;
GVL_Main.isRoboTel := HostName = 'CX-4E6032';
start := GVL_Main.start_switch;
stop := GVL_Main.stop_switch;

power_OK := GVL_main.power_available;
phase_available := GVL_Main.phase_available;
IF GVL_Main.start_switch THEN
	HydraulicsControl.start := TRUE;
	// SimpleLog(Message := 'manual Hydraulics start');
END_IF
IF GVL_Main.stop_switch THEN
	HydraulicsControl.stop := TRUE;
	// SimpleLog(Message := 'manual Hydraulics stop');
END_IF
reset := GVL_Main.reset_switch OR GVL_pendant_control.reset_switch;		
key_switch := GVL_Main.key_switch_remote;
brake_clearing := GVL_Main.brake_clearing;
cabinet_temperature := INT_TO_REAL(GVL_Main.main_cabinet_temperature) / 100.0;

// telescope is ready when power available and hydraulics ok
ready := power_OK AND HydraulicsControl.oil_pressure_OK;
IF ready THEN
	// SimpleLog(Message := 'power and Hydraulics ready');
END_IF
		
TelescopeControl();	//
CoverControl();	//
PendantControl();	//
NasmythControl();	//
HydraulicsControl();	//
// deactivate noisy fan if we are on manual
ElevationControl();	//
AzimuthElevationControl();	//
DerotatorControl();	//
FilterWheelControl();	//
FocusControl();
AuxiliaryControl(); //
MQTTCommunication();


GVL_Main.softstart_trafo := key_switch AND NOT power_failure;
power_timer(bStart := key_switch, bStop := power_OK);
power_watchdog(IN := GVL_Main.softstart_trafo AND NOT power_OK,
				PT := T#2S,
				Q => power_failure);
warn_blink(ENABLE := HydraulicsControl.oil_warning OR HydraulicsControl.oil_hot, 
	Q => warning);
error := power_failure OR 
			HydraulicsControl.pressure_failure 
			OR (NOT phase_available)
			OR GVL_Telescope.azimut_limit_switch
			//OR PendantControl.error
			OR ElevationControl.error
			OR FilterWheelControl.Error
			OR Focuscontrol.Error
			OR NasmythControl.error
			OR CoverControl.Error
			OR AzimuthElevationControl.error
			OR DerotatorControl.Error
			OR GVL_Safety.Error;
// set control lights					
GVL_Main.lamp_ready := ready;
GVL_Main.lamp_error := error OR warning;
GVL_Main.lamp_power := power_ok AND phase_available;
SafetyHandling();

ups_input_voltage 		:= GVL_MAIN.ups_input_voltage / 1000.0;
ups_input_current 		:= GVL_MAIN.ups_input_current / 1000.0;
ups_output_voltage 		:= GVL_MAIN.ups_output_voltage / 1000.0;
ups_output_current 		:= GVL_MAIN.ups_output_current / 1000.0;
ups_temperature 		:= GVL_MAIN.ups_temperature / 10.0;
ups_battery_temperature := GVL_MAIN.ups_battery_temperature / 10.0;


MQTTTimer(IN:=TRUE);
IF MQTTTimer.Q THEN // publish new payload every second
	MQTTTimer(IN:=FALSE);
	sPayloadPub := 'electronics,location=base,host=CX-22747A,module=MAIN ';
	sPayloadPub := APPEND(sPayloadPub, 'MainCabinetTemperature=', LREAL_TO_FMTSTR(cabinet_temperature, 2, TRUE));
	sPayloadPub := APPEND(sPayloadPub, ',PowerOK=', BOOL_TO_STRING(power_ok));
	sPayloadPub := APPEND(sPayloadPub, ',PhaseOK=', BOOL_TO_STRING(phase_available));
	
	MQTTCommunication.fbMqttClient.Publish(	
					sTopic:= 'STELLA1/Telemetry', 
					pPayload:= ADR(sPayloadPub), 
					nPayloadSize:= LEN2(ADR(sPayloadPub)),
					eQoS:= TcIotMqttQos.AtMostOnceDelivery,
					bRetain:= FALSE, 
					bQueue:= FALSE );

	sPayloadPub := 'electronics,location=base,host=CX-22747A,module=MAIN ';
	sPayloadPub := APPEND(sPayloadPub, 'UPSInputVoltage=', LREAL_TO_FMTSTR(ups_input_voltage, 3, TRUE));
	sPayloadPub := APPEND(sPayloadPub, ',UPSInputCurrent=', LREAL_TO_FMTSTR(ups_input_current, 3, TRUE));
	sPayloadPub := APPEND(sPayloadPub, ',UPSOutputVoltage=', LREAL_TO_FMTSTR(ups_output_voltage, 3, TRUE));
	sPayloadPub := APPEND(sPayloadPub, ',UPSOutputCurrent=', LREAL_TO_FMTSTR(ups_output_current, 3, TRUE));
	sPayloadPub := APPEND(sPayloadPub, ',UPSTemperature=', LREAL_TO_FMTSTR(ups_temperature, 1, TRUE));
	sPayloadPub := APPEND(sPayloadPub, ',UPSBatteryTemperature=', LREAL_TO_FMTSTR(ups_battery_temperature, 1, TRUE));
	
	
	MQTTCommunication.fbMqttClient.Publish(	
					sTopic:= 'STELLA1/Telemetry', 
					pPayload:= ADR(sPayloadPub), 
					nPayloadSize:= LEN2(ADR(sPayloadPub)),
					eQoS:= TcIotMqttQos.AtMostOnceDelivery,
					bRetain:= FALSE, 
					bQueue:= FALSE );

END_IF

PowerEvent(	
	Trigger := 		power_failure, 
	Level := 		ADSLOG_MSGTYPE_ERROR,
	FormatString :=	'%s',
	OnMessage := 	'Servo power FAILURE!',
	OffMEssage := 	'Servo power OK');
PowerLog(	
	Trigger := 		power_OK, 
	Level := 		ADSLOG_MSGTYPE_HINT,
	FormatString :=	'%s',
	OnMessage := 	'Power is ON',
	OffMEssage := 	'Power is OFF');
PhaseLog(	
	Trigger := 		NOT phase_available, 
	Level := 		ADSLOG_MSGTYPE_ERROR,
	FormatString :=	'%s',
	OnMessage := 	'electrical phases:	ERROR!',
	OffLevel := 	ADSLOG_MSGTYPE_HINT,
	OffMEssage := 	'electrical phases:	active');

TemperatureEvent(	
	Trigger := 		cabinet_temperature > 40.0, 
	Level := 		ADSLOG_MSGTYPE_WARN,
	FormatString :=	'Main Cabinet Temperature warning:	%s',
	OnMessage := 	LREAL_TO_FMTSTR( cabinet_temperature, 1, TRUE),);]]></ST>
    </Implementation>
    <LineIds Name="MAIN">
      <LineId Id="57" Count="36" />
      <LineId Id="95" Count="4" />
      <LineId Id="101" Count="4" />
      <LineId Id="107" Count="16" />
      <LineId Id="125" Count="1" />
      <LineId Id="128" Count="17" />
      <LineId Id="147" Count="59" />
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>